
&НаСервере
&После("ЗаполнитьВалюты")
// Дополняеть табличную часть списком валют, курс которых загружается с сайта БНБ
//
Процедура БНБ_ЗаполнитьВалюты()
	
	СписокВалют = Объект.СписокВалют;	
	БНБ_ЗагружаемыеВалюты = БНБ_РаботаСКурсамиВалют.БНБ_ЗагружаемыеВалюты();
	
	Для Каждого ЭлементВалюта Из БНБ_ЗагружаемыеВалюты Цикл
		ДобавитьВалютуВСписок(ЭлементВалюта);
	КонецЦикла;
КонецПроцедуры 

&НаСервере
//Откатывает дополнение табличной части, выполненное в процедуре БНБ_ЗаполнитьВалюты().
//
Процедура БНБ_ВосстановитьСписокВалют()
    
	СписокВалют = Объект.СписокВалют;
	СписокВалют.Очистить();
	
	ЗагружаемыеВалюты = РаботаСКурсамиВалют.ЗагружаемыеВалюты();
	
	Для Каждого ЭлементВалюта Из ЗагружаемыеВалюты Цикл
		ДобавитьВалютуВСписок(ЭлементВалюта);
	КонецЦикла;
   	
КонецПроцедуры	
	
&НаКлиенте
&Перед("НачатьЗагрузкуКурсовВалют")
// Вызывает серверную процедуру получения курсов для котируемых к леву валют 
//
Процедура БНБ_НачатьЗагрузкуКурсовВалют()
	
	// Перед загрузкой курсов валют в штатном режиме, выполняем загрузку курсов валют для тех валют, которые квотируются к леву.
	
	//Сформировать параметры загружаемых валют и даты интервала	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("НачалоПериода", Объект.НачалоПериодаЗагрузки);
	ПараметрыЗагрузки.Вставить("КонецПериода", Объект.ОкончаниеПериодаЗагрузки);
	ПараметрыЗагрузки.Вставить("СписокВалют", Объект.СписокВалют);
		
	// Передать из формы на сервер список валют и интервал дат 
	БНБ_НачатьПолучениеКурсов(ПараметрыЗагрузки);
	
	// Удалить из списка загружаемых валют валюты, которые не загружаются в штатном режиме
	БНБ_ВосстановитьСписокВалют();
    // Продолжить выполнение стандартной процедуры получения курсов
КонецПроцедуры   

&НаСервере
// Контроллер, отбирающий котируемые к леву валюты и вызывающий для них загрузку курсов
// В качестве параметров принимает структуру с ключами:
// 	СписокВалют: 	Структура с ссылками на загружаемые валюты
// 	НачалоПериода: 	Дата, указанная в форме как НачалоПериодаЗагрузки
// 	КонецПериода:	Дата, указанная в форме как ОкончаниеПериодаЗагрузки
//
Процедура БНБ_НачатьПолучениеКурсов(ПараметрыПолучения) Экспорт
	Для Каждого Валюта из ПараметрыПолучения.СписокВалют Цикл		
		Если Валюта.Валюта.БНБ_КотироватьКБолгарскомуЛеву И Валюта.Загружать Тогда
			БНБ_РаботаСКурсамиВалют.БНБ_СкачатьКурсы(Валюта.Валюта, ПараметрыПолучения.НачалоПериода, ПараметрыПолучения.КонецПериода);	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

	
